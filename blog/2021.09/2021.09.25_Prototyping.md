!!title Prototyping
!!summary Prototyping a 3rd person game in Godot.
!!keywords godot third person 3rd camera mouse controller
!!series godot

## 0743 - Creating a game

I've mentioned in the past I'd like to make a game. I've also made a point of saying 'write it twice'... So I started doing that. Godot is a free game engine I've used in the past, so I figured I'd start there.

The node based structure makes it easy to get things going and the scripting language is pretty easy to grasp. I was able to hack the resulting code together in a few hours. 

```
extends Spatial

onready var target = $tall_thin_female
onready var camera = $Camera

var sensitivity = 3.0
var prev_mouse_pos = Vector2(0,0)
var mouse_pos = Vector2(0,0)
var win_size = Vector2(10,10)
var apply_deadzone = false
var lock_mouse = true

func _ready():
	win_size = get_viewport().size
	
func _input(event):
	win_size = get_viewport().size
	win_size.x = max(win_size.x, 1)
	win_size.y = max(win_size.y, 1)
	
	var local_dir = Vector3(0,0,0);
	
	if event is InputEventMouseMotion:
		prev_mouse_pos = mouse_pos
		mouse_pos = event.position
	elif event is InputEventMouseButton:
		if event.button_index == BUTTON_RIGHT:
			apply_deadzone = event.pressed		
	elif event is InputEventKey:
		if event.pressed:
			if event.scancode == KEY_W:
				local_dir.z += 1
			elif event.scancode == KEY_S:
				local_dir.z -= 1
			elif event.scancode == KEY_A:
				local_dir.x += 1
			elif event.scancode == KEY_D:
				local_dir.x -= 1
			elif event.scancode == KEY_ESCAPE:
				lock_mouse = !lock_mouse

	target.set_translation(target.translation + local_dir)
	
func _process(delta):
	
	if !apply_deadzone && lock_mouse:
		get_viewport().warp_mouse(win_size / 2.0)
	
	if apply_deadzone || lock_mouse:
		set_camera_rotation(apply_deadzone, mouse_pos, win_size)


# returns the normalized horizontal rotation. Direction agnostic.
func calculate_deadzone_rotation(deadzone_len, win_len, mouse_coordinate):
	var deadzone_l = deadzone_len 
	var deadzone_r = win_len - deadzone_len
	
	if mouse_coordinate <= deadzone_l:
		return 1.0 - (mouse_coordinate / deadzone_len)
	
	if mouse_coordinate >= deadzone_r:
		return (mouse_coordinate - deadzone_r) / deadzone_len * -1.0

	return 0.0
	
func calculate_normal_rotation(mouse_coordinate, win_len):
	var center_screen = win_len / 2.0
	
	var delta = center_screen - mouse_coordinate
	return delta / win_len

func set_camera_rotation(apply_deadzone, mouse_pos, win_size):
	var deadzone_horz_ratio = 0.1
	var deadzone_vert_ratio = 0.1
		
	var deadzone_h = win_size.y * deadzone_vert_ratio 
	var deadzone_w = win_size.x * deadzone_horz_ratio
	
	var sensitivity = 0.5
	
	var horz_sensitivity = sensitivity
	var vert_sensitivity = sensitivity
	
	var horz_deadzone_sensitivity = 0.025
	var vert_deadzone_sensitivity = 0.025
			
	var horz_rotation = 0
	var vert_rotation = 0
	
	# Calculate rotations
	if apply_deadzone:
		horz_rotation = calculate_deadzone_rotation(deadzone_w, win_size.x, mouse_pos.x)
		vert_rotation = calculate_deadzone_rotation(deadzone_h, win_size.y, mouse_pos.y)
		
		horz_rotation *= horz_deadzone_sensitivity
		vert_rotation *= vert_deadzone_sensitivity
	else:
		horz_rotation = calculate_normal_rotation(mouse_pos.x, win_size.x)
		vert_rotation = calculate_normal_rotation(mouse_pos.y, win_size.y)
		horz_rotation *= horz_sensitivity
		vert_rotation *= vert_sensitivity

	
	# Create new rotation
	var new_rotation = camera.rotation + Vector3(vert_rotation, horz_rotation, 0)
	
	# Clamp it so doesn't spin
	var MAX_X_ROT = 0.5
	var MIN_X_ROT = -1.0
	new_rotation.x = clamp(new_rotation.x, MIN_X_ROT, MAX_X_ROT)
	
	camera.set_rotation(new_rotation)
```

What that does is it enables a sort of Resident Evil 4 or Dead Space aiming. Granted, this is not optimal. I've hardcoded in keys and haven't figured out the camera physics yet. But it is excellent for getting something together quickly. When (or if) I decide to rewrite the engine, I can simply copy the algorithms over to get what I want.

Here's a video:

![Render](./_img/2021.09/2021.09.25_0756.gif)


I've also been tweaking `blog_gen` to get it more responsive. It's not perfect, but it's functional. It is certainly giving me ideas for some sort of virtual machine for a UI.

## 0908

I've gotten CI working.